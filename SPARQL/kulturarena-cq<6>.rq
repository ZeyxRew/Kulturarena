PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX kul: <http://example.org/kulturarena/>
SELECT ?musician ?yearsApart
WHERE {
  ?concert1 rdf:type kul:Concert;
            kul:performedBy ?musician;
            kul:inYear ?year1.
  
  ?concert2 rdf:type kul:Concert;
            kul:performedBy ?musician;
            kul:inYear ?year2.
  
  FILTER(?concert1 != ?concert2 && ?year1 > ?year2)
  FILTER NOT EXISTS{
    ?concert3 rdf:type kul:Concert;
            kul:performedBy ?musician;
            kul:inYear ?year3.
    FILTER(?year1 > ?year3 && ?year3 > ?year2)
  }
  BIND((xsd:integer(?year1) - xsd:integer(?year2)) AS ?yearsApart) 
}GROUP BY ?musician ?yearsApart
ORDER BY DESC(?yearsApart)
LIMIT 1

